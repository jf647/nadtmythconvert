#!/usr/bin/perl
#

use strict;
use warnings;

use FindBin;
use lib "$FindBin::Bin/../lib";

use Getopt::Long;
use NADTMythTV;
use NADTMythTV::Util;
use YAML;

# parse args
my %opts = (
  verbose => 0,
  debug => 0,
);
my @origargs = @ARGV;
GetOptions(
  \%opts,
  'verbose!',
  'debug!',
);

# load config
my $cfg = NADTMythTV->cfg;

# get logging
my $log = NADTMythTV->log( verbose => $opts{verbose}, debug => $opts{debug} );
$log->debug("orig args: @origargs");
$log->info('START');

# set up MythTV
my $nadtmyth = NADTMythTV->new;

# set up database
my $mythdb = $nadtmyth->mythdb;
my $converted_rs = $mythdb->resultset('NadtmythConverted');
my $recorded_rs = $mythdb->resultset('Recorded');
my $publish_history_rs = $mythdb->resultset('NadtmythPublishHistory');

# get a lock
NADTMythTV::Util->lock;

# get list of converted programs where there is no to_publish record
my $converts = $converted_rs->search(
  { 'to_publish.complete' => [ 0, undef ], 'recorded.autoexpire' => { '!=' => 9999 } },
  { join => [ 'to_publish', 'recorded' ] }
);
while( my $convert = $converts->next ) {
  my $orig = $recorded_rs->single( { chanid => $convert->chanid, starttime => $convert->starttime } );
  unless( $orig ) {
    $log->warn("can't find source program for converted id ", $convert->id);
    next;
  }
  my $fulltitle = NADTMythTV::Util->fulltitle( $orig );
  my $starttime = $orig->starttime;
  for my $matchprofile( @{ $cfg->{publish}->{match} } ) {
    my $rx = $matchprofile->{regex};
    if( $convert->destformat eq $matchprofile->{format} && $fulltitle =~ m/$rx/ ) {

      $log->debug("considering $fulltitle at $starttime");
    
      # perform duplicate detection
      my $recprofile = $orig->record;
      my $dupmethod = 6;
      if( $recprofile ) {
        $dupmethod = $recprofile->dupmethod;
      }
      my %searchparams;
      my $store = 1;
      my $not_new = 0;
      # dupmethod 1 is no duplicate checking
      if( 1 == $dupmethod ) {
        $log->debug("$fulltitle at $starttime recording profile has duplicate detection disabled");
        $store = 0;
      }
      # dupmethod 2 is subtitle only
      elsif( 2 == $dupmethod ) {
        %searchparams = ( title => $orig->title, subtitle => $orig->subtitle );
      }
      # dupmethod 4 is description only
      elsif( 4 == $dupmethod ) {
        %searchparams = ( title => $orig->title, description => $orig->description );
      }
      # dupmethod 6 is subtitle and description
      # dupmethod 8 is subtitle then desc - treat like 6
      elsif( 6 == $dupmethod || 8 == $dupmethod ) {
        %searchparams = ( title => $orig->title, subtitle => $orig->subtitle, description => $orig->description );
      }
      else {
        $log->logdie("unexpected duplicate detection method $dupmethod");
      }
      if( %searchparams ) {
        my $count = $publish_history_rs->search( \%searchparams )->count;
        if( 0 != $count ) {
          $log->info("$fulltitle at $starttime has already been published");
          $not_new = 1;
        }
      }
      
      # create the to_publish record if needed
      my $to_publish;
      if( $not_new ) {
        if( $to_publish = $convert->to_publish ) {
          $log->debug("found existing to_publish record for $fulltitle at $starttime");
        }
        else {
          $log->info("not creating a new to_publish record for $fulltitle at $starttime (found in history)");
        }
      }
      else {
        if( $to_publish = $convert->to_publish ) {
          $log->debug("found existing to_publish record for $fulltitle at $starttime");
        }
        else {
          $to_publish = $convert->create_related( to_publish => {} );
          $log->info("created to_publish record for $fulltitle at $starttime");
        }
      }

      # create a publish_history record
      if( $store ) {
        $publish_history_rs->create( {
          chanid => $orig->chanid,
          starttime => $orig->starttime,
          title => $orig->title,
          subtitle => $orig->subtitle,
          description => $orig->description,
        } );
      }

      # create to_publish_dest records if needed
      for my $dest( keys %{ $matchprofile->{dest} } ) {
        my $to_publish_dest;
        my $dests = $to_publish->search_related( to_publish_dest => { dest => $dest } );
        if( $dests->count ) {
          $to_publish_dest = $dests->first;
          $log->debug("found existing to_publish_dest record for $dest");
        }
        else {
          $to_publish_dest = $to_publish->create_related( to_publish_dest => { dest => $dest, purge_days => $matchprofile->{dest}->{$dest}->{keepdays} } );
          $log->info("adding dest for $dest");
        }

        # create to_publish_inform records if needed
        for my $email( @{ $matchprofile->{dest}->{$dest}->{inform} } ) {
          my $to_publish_inform;
          my $informs = $to_publish_dest->search_related( to_publish_inform => { email => $email } );
          if( $informs->count ) {
            $to_publish_inform = $informs->first;
            $log->debug("found existing to_publish_inform for $email; not creating another");
          }
          else {
            $to_publish_inform = $to_publish_dest->create_related( to_publish_inform => { email => $email } );
            $log->info("adding inform for $email");
          }
        }
      }

    }
  }
}

# commit database
$mythdb->txn_commit;

# all done
NADTMythTV::Util->unlock;
$log->info('STOP');
exit(0);

#
# EOF
